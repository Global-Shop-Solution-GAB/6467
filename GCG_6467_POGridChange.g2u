Program.Sub.Preflight.Start
V.Global.bConOpen.Declare(Boolean, False)
Program.Sub.Preflight.End

Program.Sub.Main.Start
'6467: Add Inventory user Field 2 to PO Receive Purchase Order Items Screen 
'Author: Brandon Medina 
'Quote: 13065-0
'Customer: Orchard-Rite 
F.Intrinsic.Control.Try
	F.Intrinsic.Control.SelectCase(V.Caller.Hook)
		'PO Receipt Populate 
		F.Intrinsic.Control.Case(15020)
			F.Intrinsic.Control.CallSub(ShowColumn)
		'PO Receipt Post Process 
		F.Intrinsic.Control.Case(15021)
			F.Intrinsic.Control.CallSub(ModBDF)
	F.Intrinsic.Control.EndSelect
F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.ErrorMsg.Start
V.Local.sError.Declare

F.Intrinsic.Control.If(V.Global.bConOpen)
	F.ODBC.Connection!con.Close
	V.Global.bConOpen.Set(False)
F.Intrinsic.Control.EndIf
'Generic Error message.
f.Intrinsic.String.Build("Project GCG_6467_POGridChange.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.ShowColumn.Start
F.Intrinsic.Control.Try
	'Show User 2 column  
	F.Intrinsic.BDF.Load("LineItems", "AUX001")
	F.Intrinsic.BDF.SetColumnTitle("LineItems", 6, "Drawing #")
	F.Intrinsic.BDF.Save("LineItems", "AUX001")


F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, V.Ambient.CurrentSubroutine)
F.Intrinsic.Control.EndTry
Program.Sub.ShowColumn.End

Program.Sub.ModBDF.Start
F.Intrinsic.Control.Try
	V.Local.iRowCount.Declare
	V.Local.i.Declare
	V.Local.sPart.Declare
	V.Local.sPONum.Declare
	V.Local.sSQL.Declare
	V.Local.sRowContent.Declare
	
	'When new PO selected, grab info from User 2 field Inv Master 
	'INVENTORY_MST2.TEXT_INFO2 
	'Passed: 
	'000002: PO-NUM (Needs padding)
	
	
	'Create dictionary to house all lines from PO on screen. Want to query and grab all User field 2 
	F.Intrinsic.String.LPad(V.Passed.000002, "0", 7, V.Local.sPONum)
	F.ODBC.Connection!con.OpenCompanyConnection
	V.Global.bConOpen.Set(True)
	F.Intrinsic.String.Build("select rtrim(A.PART), B.TEXT_INFO2 from V_PO_LINES A left join INVENTORY_MST2 B on A.PART = B.PART and A.LOCATION = B.LOCATION where A.PURCHASE_ORDER = '{0}'", V.Local.sPONum, V.Local.sSQL)
	F.Data.Dictionary.CreateFromSQL("dictPartUser2", "con", V.Local.sSQL)
	F.ODBC.Connection!con.Close
	V.Global.bConOpen.Set(False)
	
	F.Intrinsic.BDF.Load("LineItems", "AUX001")
	F.Intrinsic.BDF.Clone("LineItems", "LineItemsCopy")
	F.Intrinsic.BDF.ReadRowCount("LineItems", V.Local.iRowCount)
	F.Intrinsic.Math.Sub(V.Local.iRowCount, 1, V.Local.iRowCount)

	F.Intrinsic.Control.For(V.Local.i, 0, V.Local.iRowCount, 1)
		'Read part number from grid 
		'F.Intrinsic.BDF.TextMatrix("LineItemsCopy", 4, V.Local.i, V.Local.sPart)
		F.Intrinsic.BDF.ReadRow("LineItems", V.Local.i, V.Local.sRowContent)
		F.Intrinsic.String.Split(V.Local.sRowContent, "|~|", V.Local.sRowContent)
		'Index 6 
		V.Local.sPart.Set(V.Local.sRowContent(1))
		F.Intrinsic.String.RTrim(V.Local.sPart, V.Local.sPart)
		V.Local.sRowContent(6).Set(V.Dictionary.dictPartUser2![V.Local.sPart])
		F.Intrinsic.String.Join(V.Local.sRowContent, "|~|", V.Local.sRowContent)
		F.Intrinsic.BDF.WriteRow("LineItemsCopy", V.Local.sRowContent)
	F.Intrinsic.Control.Next(V.Local.i)
	
	F.Data.Dictionary.Close("dictPartUser2")
	'F.Intrinsic.BDF.SetColumnTitle("LineItemsCopy", 6, "USER2")
	F.Intrinsic.BDF.Save("LineItemsCopy", "AUX001")

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, V.Ambient.CurrentSubroutine)
F.Intrinsic.Control.EndTry
Program.Sub.ModBDF.End

Program.Sub.Comments.Start
${$5$}$3.0.0.0$}$1
${$6$}$bmedina$}$20210820155816470$}$kOipCrsyLBVj2KoSwkcdK23ChxGVcBlEarwrywx22hEiH9nExL32a66bs1nNm54D/8MqKTQ7XH8=
Program.Sub.Comments.End